// Здесь находится «стандартная» библиотека Хорошего Учебного Языка.

// СДЕЛАТЬ: ввести функцию печать_строки, которая делает системный вызов SYS_write
// Хотя, мы тогда теряем возможность печатать строки в режиме интерпретации.

про печать_нат(число: нат) нч
    если число = 0нат нч
        печать(«0»);
        вернуть;
    кц

    конст ОБЪЁМ := 32;
    пер буфер: массив(ОБЪЁМ, нат8);
    пер размер: нат := 0нат;

    пока число +? 0нат нч
        // СДЕЛАТЬ: размер += 1;
        размер := размер + 1нат;
        буфер(ОБЪЁМ как нат - размер) := (число ост 10нат + 48нат) как нат8; // СДЕЛАТЬ: буквенные литералы
        // СДЕЛАТЬ: число /= 10;
        число := число / 10нат;
    кц

    печать(срез(буфер, ОБЪЁМ как нат - размер, размер));
кц

про печать_цел(число: цел) нч
    если число -? 0 нч
        печать(«-»);
        число := -число;
    кц
    печать_нат(число как нат);
кц

про печать_лог(условие: лог) нч
    если условие нч
        печать(«истина»);
        вернуть;
    кц
    печать(«ложь»);
кц

про это_пробел(символ: нат8): лог нч
    пер таблица: строка := « \т\н»;
    для индекс := 0..размер(таблица)-1
    то если таблица(индекс) = символ
       то вернуть истина;
    вернуть ложь;
кц

про подбрить_строку_слева(стр: строка): строка нч
    пер начало: цел := 0;
    пока начало -? размер(стр) и это_пробел(стр(начало)) нч
        начало := начало + 1;
    кц
    вернуть срез(стр, начало как нат, (размер(стр) - начало) как нат);
кц

про верхний_регистр_аскии(символ: нат8): нат8 нч
    конст МАЛЕНЬКАЯ_ЭЙ  := 97;
    конст МАЛЕНЬКАЯ_ЗЕТ := 122;
    конст БОЛЬШАЯ_ЭЙ    := 65;
    если МАЛЕНЬКАЯ_ЭЙ -?= символ как цел и символ как цел -?= МАЛЕНЬКАЯ_ЗЕТ нч
        вернуть (символ как цел - МАЛЕНЬКАЯ_ЭЙ + БОЛЬШАЯ_ЭЙ) как нат8;
    кц
    вернуть символ;
кц
