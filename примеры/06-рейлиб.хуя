вкл рейлиб;

// СДЕЛАТЬ: шестнадцатеричные литералы
конст ЦВЕТ_ФОНА    = 4279769112; // 0xFF181818
конст ЦВЕТ_КРАСНЫЙ = 4278190335; // 0xFF0000FF

конст ШИРИНА = 50;
конст ВЫСОТА = 50;

пер икс: вещ;
пер игрек: вещ;
пер новый_икс: вещ;
пер новый_игрек: вещ;
пер дельта_икс: вещ;
пер дельта_игрек: вещ;
пер дельта_времени: вещ;
пер ширина_экрана: вещ;
пер высота_экрана: вещ;

про главная() нч
    // СДЕЛАТЬ: вещественные литералы
    икс          := 0 как вещ;
    игрек        := 0 как вещ;
    дельта_икс   := 100 как вещ;
    дельта_игрек := 100 как вещ;
    открыть_окно(800, 600, адрес(«Привет, Raylib!»));
    установить_целевую_частоту_кадров(60);
    пока не пора_закрыть_окно() нч
        дельта_времени := получить_время_кадра();
        ширина_экрана := получить_ширину_экрана() как вещ;
        высота_экрана := получить_высоту_экрана() как вещ;

        новый_икс := икс + дельта_икс*дельта_времени;
        если новый_икс мн 0 как вещ или новый_икс + ШИРИНА как вещ бл ширина_экрана
        то дельта_икс := -дельта_икс;
        иначе то икс := новый_икс;

        новый_игрек := игрек + дельта_игрек*дельта_времени;
        если новый_игрек мн 0 как вещ или новый_игрек + ВЫСОТА как вещ бл высота_экрана
        то дельта_игрек := -дельта_игрек;
        иначе то игрек := новый_игрек;

        начать_рисовать();
        очистить_фон(ЦВЕТ_ФОНА);
        нарисовать_прямоугольник(икс как цел, игрек как цел, ШИРИНА, ВЫСОТА, ЦВЕТ_КРАСНЫЙ);
        закончить_рисовать();
    кц
    закрыть_окно();
кц
