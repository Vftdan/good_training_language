вкл рейлиб;

// СДЕЛАТЬ: шестнадцатеричные литералы
конст ЦВЕТ_ФОНА    = 4279769112; // 0xFF181818
конст ЦВЕТ_КРАСНЫЙ = 4278190335; // 0xFF0000FF
конст ОБЪЁМ = 10;

конст ШИРИНА = 50;
конст ВЫСОТА = 50;

// ВАЖНО! Т.к. мы не выравниваем структуры так, как это делает Си, эта
// структура не может быть передана в Рейлиб.
структ Вектор2 нч
    икс: вещ;
    игрек: вещ;
кц

пер позиции: массив(ОБЪЁМ, Вектор2);
пер новая_позиция: Вектор2;
пер дельты: массив(ОБЪЁМ, Вектор2);
пер дельта_времени: вещ;
пер ширина_экрана: вещ;
пер высота_экрана: вещ;
пер пауза: лог;

про случайное_вещественное(): вещ то
    вернуть получить_случайное_число(0, 100) как вещ / 100.0;

про главная() нч
    пер индекс: цел;

    установить_случайное_семя(69);

    пауза := ложь;

    индекс := 0;
    пока индекс мн ОБЪЁМ нч
        позиции(индекс).икс   := случайное_вещественное()*50.0;
        позиции(индекс).игрек := случайное_вещественное()*10.0;
        дельты(индекс).икс    := случайное_вещественное()*400.0;
        дельты(индекс).игрек  := случайное_вещественное()*200.0;
        индекс := индекс + 1;
    кц
    открыть_окно(800, 600, адрес(«Привет, Raylib!»));
    установить_целевую_частоту_кадров(60);
    пока не пора_закрыть_окно() нч
        дельта_времени := получить_время_кадра();
        ширина_экрана := получить_ширину_экрана() как вещ;
        высота_экрана := получить_высоту_экрана() как вещ;

        если нажата_клавиша(КЛАВИША_ПРОБЕЛ) то пауза := не пауза;

        начать_рисовать();
        очистить_фон(ЦВЕТ_ФОНА);
            индекс := 0;
            пока индекс мн ОБЪЁМ нч
                если не пауза нч
                    новая_позиция.икс := позиции(индекс).икс + дельты(индекс).икс*дельта_времени;
                    если новая_позиция.икс мн 0.0 или новая_позиция.икс + ШИРИНА как вещ бл ширина_экрана
                    то дельты(индекс).икс := -(дельты(индекс).икс); // СДЕЛАТЬ: можно ли как-то распарсить чтобы не нужны были скобки?
                    иначе то позиции(индекс).икс := новая_позиция.икс;

                    новая_позиция.игрек := позиции(индекс).игрек + дельты(индекс).игрек*дельта_времени;
                    если новая_позиция.игрек мн 0.0 или новая_позиция.игрек + ВЫСОТА как вещ бл высота_экрана
                    то дельты(индекс).игрек := -(дельты(индекс).игрек);
                    иначе то позиции(индекс).игрек := новая_позиция.игрек;
                кц

                нарисовать_прямоугольник(позиции(индекс).икс как цел,
                                         позиции(индекс).игрек как цел,
                                         ШИРИНА, ВЫСОТА, ЦВЕТ_КРАСНЫЙ);

                индекс := индекс + 1;
            кц
        закончить_рисовать();
    кц
    закрыть_окно();
кц
