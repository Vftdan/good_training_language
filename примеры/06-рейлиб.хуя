вкл рейлиб;

// СДЕЛАТЬ: шестнадцатеричные литералы
конст ЦВЕТ_ФОНА    = 4279769112; // 0xFF181818
конст ЦВЕТ_КРАСНЫЙ = 4278190335; // 0xFF0000FF
конст ОБЪЁМ = 10;

конст ШИРИНА = 50;
конст ВЫСОТА = 50;

пер индекс: цел;
пер иксы: массив(ОБЪЁМ, вещ);
пер игреки: массив(ОБЪЁМ, вещ);
пер новый_икс: вещ;
пер новый_игрек: вещ;
пер дельта_иксы: массив(ОБЪЁМ, вещ);
пер дельта_игреки: массив(ОБЪЁМ, вещ);
пер дельта_времени: вещ;
пер ширина_экрана: вещ;
пер высота_экрана: вещ;

про случайное_вещественное(): вещ то
    вернуть получить_случайное_число(0, 100) как вещ / 100.0;

про главная() нч
    установить_случайное_семя(69);

    индекс := 0;
    пока индекс мн ОБЪЁМ нч
        иксы(индекс)          := случайное_вещественное()*50.0;
        игреки(индекс)        := случайное_вещественное()*10.0;
        дельта_иксы(индекс)   := случайное_вещественное()*400.0;
        дельта_игреки(индекс) := случайное_вещественное()*200.0;
        индекс := индекс + 1;
    кц
    открыть_окно(800, 600, адрес(«Привет, Raylib!»));
    установить_целевую_частоту_кадров(60);
    пока не пора_закрыть_окно() нч
        дельта_времени := получить_время_кадра();
        ширина_экрана := получить_ширину_экрана() как вещ;
        высота_экрана := получить_высоту_экрана() как вещ;

        начать_рисовать();
        очистить_фон(ЦВЕТ_ФОНА);
            индекс := 0;
            пока индекс мн ОБЪЁМ нч
                новый_икс := иксы(индекс) + дельта_иксы(индекс)*дельта_времени;
                если новый_икс мн 0.0 или новый_икс + ШИРИНА как вещ бл ширина_экрана
                то дельта_иксы(индекс) := -дельта_иксы(индекс);
                иначе то иксы(индекс) := новый_икс;

                новый_игрек := игреки(индекс) + дельта_игреки(индекс)*дельта_времени;
                если новый_игрек мн 0.0 или новый_игрек + ВЫСОТА как вещ бл высота_экрана
                то дельта_игреки(индекс) := -дельта_игреки(индекс);
                иначе то игреки(индекс) := новый_игрек;

                нарисовать_прямоугольник(иксы(индекс) как цел, игреки(индекс) как цел, ШИРИНА, ВЫСОТА, ЦВЕТ_КРАСНЫЙ);

                индекс := индекс + 1;
            кц
        закончить_рисовать();
    кц
    закрыть_окно();
кц
