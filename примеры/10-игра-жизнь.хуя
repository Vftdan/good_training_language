вкл прелюдия;

конст ВЫСОТА = 10;
конст ШИРИНА = 20;

пер поле: массив(ВЫСОТА, массив(ШИРИНА, лог));
пер скрытое_поле: массив(ВЫСОТА, массив(ШИРИНА, лог));

про инициализировать_поле() нч
    для пер строка := 1..ВЫСОТА то
        для пер столбец := 1..ШИРИНА то
            поле(строка - 1)(столбец - 1) := ложь;
    поле(0)(1) := истина;
    поле(1)(2) := истина;
    поле(2)(0) := истина;
    поле(2)(1) := истина;
    поле(2)(2) := истина;
кц

про отобразить_поле() нч
    для пер строка := 1..ВЫСОТА нч
        для пер столбец := 1..ШИРИНА нч
            если поле(строка - 1)(столбец - 1)
            то печать(«%»);
            иначе то печать(«_»);
        кц
        печать(«\н»);
    кц
кц

про подсчитать_соседей(строка0: цел, столбец0: цел): цел нч
    пер соседи : цел = 0;
    для пер дстр := -1..1 то
    для пер дстл := -1..1 то
    если не (дстр == 0 и дстл == 0) нч
        пер строка: цел = строка0 + дстр;
        пер столбец: цел = столбец0 + дстл;
        // СДЕЛАТЬ: Биноп для «меньше либо равно».
        если (0 мн строка или строка == 0) и строка мн ВЫСОТА то
        если (0 мн столбец или столбец == 0) и столбец мн ШИРИНА то
        если поле(строка)(столбец) то соседи := соседи + 1;
    кц
    вернуть соседи;
кц

про следующее_поле() нч
    для пер строка := 1..ВЫСОТА то
        для пер столбец := 1..ШИРИНА нч
            пер соседи: цел = подсчитать_соседей(строка - 1, столбец - 1);
            если поле(строка - 1)(столбец - 1)
            то скрытое_поле(строка - 1)(столбец - 1) := соседи == 2 или соседи == 3;
            иначе то скрытое_поле(строка - 1)(столбец - 1) := соседи == 3;
        кц
кц

про перевернуть_поле() нч
    для пер строка := 1..ВЫСОТА то
        для пер столбец := 1..ШИРИНА то
            поле(строка - 1)(столбец - 1) := скрытое_поле(строка - 1)(столбец - 1);
кц

про главная() нч
    инициализировать_поле();
    отобразить_поле();

    для пер _ := 1..10 нч
        следующее_поле();
        перевернуть_поле();
        печать(«\н»);
        отобразить_поле();
    кц
кц
